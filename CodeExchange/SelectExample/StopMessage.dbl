subroutine StopMessage
    endparams
    record
        ttCh    ,i4
proc
    ttCh = %syn_freechn()
    onerror err
    open(ttCh, O, 'TT:')
    anyKey(ttCh, 10, "terminate program")
    close ttCh
err,
    offerror
    ;supress stop message
    flags(7000000,1)
    xreturn
endsubroutine

;; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

subroutine anyKey
    req in  ttChan      ,n                  ;channel
    opt in  waitTime    ,n                  ;number of iterations
    opt in  message     ,a                  ;message
    endparams
    record
        char        ,d4                     ;input character
        cnt         ,d2                     ;timeout countdown
        bs          ,a ^size(cnt)           ;backspace characters
        spc         ,a ^size(cnt)           ;space characters
        myTtflgs    ,d10
        myFlags     ,d10
proc
    flags(myFlags,2)
    flags(50000,1)
.ifndef DBLNET
    ttflgs(myTtflgs,2)
    ttflgs(4000,1)
.endc ;DBLNET
    fill(%char(8), bs)
    if(%passed(waitTime)) then
        cnt = waitTime
    else
        cnt = 10
    if(^passed(message) && message) then
        display(ttChan, "Press any key to "+message+" "+spc)
    else
        display(ttChan, "Press any key to continue "+spc)
    onerror ($ERR_TIMOUT) timeout, done
    accept(ttChan, char, WAIT:1)
    if(false)
    begin
timeout,
        if(cnt > 1)
        begin
            cnt -= 1
            display(ttChan, bs, ^a(cnt))
            accept(ttChan, char, WAIT:1)
        end
    end
done,
    offerror
.ifndef DBLNET
    ;; get any additional characters typed
    while(%ttsts)
        accept(ttChan, char)
.endc ;DBLNET
    display(ttChan, bs, spc, 13, 10)
.ifndef DBLNET
    ttflgs(myTtflgs,1)
.endc ;DBLNET
    flags(myFlags,1)

    xreturn
endsubroutine

;; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
