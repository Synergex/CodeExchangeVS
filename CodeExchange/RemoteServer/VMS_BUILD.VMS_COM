$! Build file for RemoteServer on OpenVMS
$!
$! PARAMETER 1 VALUES : DEBUG (build in debug mode)
$! PARAMETER 1 VALUES : START (create startup command files)
$!
$! PARAMETER 2 VALUES : START (create startup command files)
$!
$
$ !FOLDER = "DKA0:[Development.SynPSG.CodeExchange.RemoteServer.]"
$
$ ! Determine the current directory, and define DEVROOT as a rooted directory.
$ PROCEDURE = F$ENVIRONMENT("PROCEDURE")
$ FOLDER = F$PARSE(PROCEDURE,,,"DIRECTORY")
$ FOLDER = F$PARSE(PROCEDURE,,,"DEVICE") + F$EXTRACT(0,F$LOCATE("]",FOLDER),FOLDER) + ".]"
$
$ DEFINE/NOLOG/TRANSLATION_ATTRIBUTES=CONCEALED DEVROOT 'FOLDER'
$
$ ! Runtime environment variables
$ DEFINE/NOLOG REMOTESERVERLOG DEVROOT:[log]
$ DEFINE/NOLOG REMOTESERVEREXE DEVROOT:[exe]
$
$ ! RemoteServer environment variables
$ DEFINE/NOLOG RSCLNTSRC DEVROOT:[Client]
$ DEFINE/NOLOG RSSRVRSRC DEVROOT:[Server]
$ DEFINE/NOLOG CORUTLSRC DEVROOT:[Server]
$ DEFINE/NOLOG SYSNETSRC DEVROOT:[Server]
$ DEFINE/NOLOG SYNEXPDIR DEVROOT:[hdr]
$ DEFINE/NOLOG SYNIMPDIR DEVROOT:[hdr]
$ DEFINE/NOLOG OBJ DEVROOT:[obj]
$ DEFINE/NOLOG DEF DEVROOT:[Server]
$
$ ! generate prototype files
$ IF F$SEARCH("SYNEXPDIR:*.dbp") .NES. "" THEN DELETE/NOLOG SYNEXPDIR:*.dbp;*
$
$ dblproto RSCLNTSRC:*.dbl
$ dblproto RSSRVRSRC:*.dbl
$ dblproto RSSRVRSRC:*.dbc
$
$ IF "''P1'" .EQS. "DEBUG"
$ THEN
$   DBL :== DIBOL/DEBUG/STRICT/ALIGN
$   DBLINK :== LINK/DEBUG
$ ELSE
$   DBL :== DIBOL/OPTIMIZE/STRICT/ALIGN
$   DBLINK :== LINK
$ ENDIF
$
$ DBL/OBJECT=OBJ:RemoteServer RSSRVRSRC:RemoteServer.dbl
$ DBL/OBJECT=OBJ:rsProcess RSSRVRSRC:rsProcess.dbl
$ DBL/OBJECT=OBJ:rsProcessFileName RSSRVRSRC:rsProcessFileName.dbl
$ DBL/OBJECT=OBJ:rsClient RSCLNTSRC:rsClient.dbl
$
$ ! SynPSG.Core
$ DBL/OBJECT=OBJ:dbl2dibol CORUTLSRC:dbl2dibol.dbl
$ DBL/OBJECT=OBJ:endOfToken CORUTLSRC:endOfToken.dbl
$ DBL/OBJECT=OBJ:logging CORUTLSRC:logging.dbl
$ DBL/OBJECT=OBJ:stringCase CORUTLSRC:stringCase.dbl
$ DBL/OBJECT=OBJ:stringReplace CORUTLSRC:stringReplace.dbl
$ DBL/OBJECT=OBJ:pipe CORUTLSRC:pipe.dbc
$
$ ! SynPSG.System.Net
$ DBL/OBJECT=OBJ:dns SYSNETSRC:dns.dbc
$ DBL/OBJECT=OBJ:IPEndPoint SYSNETSRC:IPEndPoint.dbc
$ DBL/OBJECT=OBJ:IPHostEntry SYSNETSRC:IPHostEntry.dbc
$ DBL/OBJECT=OBJ:NetWorkStream SYSNETSRC:NetWorkStream.dbc
$ DBL/OBJECT=OBJ:Socket SYSNETSRC:Socket.dbc
$ DBL/OBJECT=OBJ:SocketException SYSNETSRC:SocketException.dbc
$ DBL/OBJECT=OBJ:TcpClient SYSNETSRC:TcpClient.dbc
$ DBL/OBJECT=OBJ:TcpListener SYSNETSRC:TcpListener.dbc
$
$ ! build client
$ DBLINK/EXECUTABLE=REMOTESERVEREXE:rsClient OBJ:rsClient, -
 OBJ:rsProcessFileName,OBJ:dns,OBJ:IPEndPoint,OBJ:IPHostEntry,OBJ:logging, -
 OBJ:NetWorkStream,OBJ:Socket,OBJ:SocketException,OBJ:stringCase, -
 OBJ:TcpClient,OBJ:TCPListener,-
 SYNERGYDE$ROOT:[DBL.BIN]SYNRTL/OPT
$
$ ! build server components
$ DBLINK/EXECUTABLE=REMOTESERVEREXE:rsProcess OBJ:rsProcess, -
 OBJ:rsProcessFileName,OBJ:dbl2dibol,OBJ:endOfToken,OBJ:IPEndPoint, -
 OBJ:logging,OBJ:NetWorkStream,OBJ:pipe,OBJ:Socket,OBJ:SocketException, -
 OBJ:stringCase,OBJ:stringReplace,OBJ:TcpClient,OBJ:TCPListener, -
 SYNERGYDE$ROOT:[DBL.BIN]SYNRTL/OPT
$ DBLINK/EXECUTABLE=REMOTESERVEREXE:RemoteServer OBJ:RemoteServer, -
 OBJ:rsProcessFileName,OBJ:IPEndPoint,OBJ:logging,OBJ:NetWorkStream, -
 OBJ:Socket,OBJ:SocketException,OBJ:stringCase,OBJ:TcpClient,OBJ:TCPListener, -
 SYNERGYDE$ROOT:[DBL.BIN]SYNRTL/OPT
$
$ IF "''P1'" .EQS. "START" .OR. "''P2'" .EQS. "START"
$ THEN
$
$ PROCEDURE = F$ENVIRONMENT("PROCEDURE")
$ FOLDER = F$PARSE(PROCEDURE,,,"DIRECTORY")
$ FOLDER = F$PARSE(PROCEDURE,,,"DEVICE") + F$EXTRACT(0,F$LOCATE("]",FOLDER),FOLDER)
$
$ ! create startup command files
$ OPEN/WRITE STARTFILE REMOTESERVEREXE:START_REMOTE_SERVER.COM
$ WRITE STARTFILE "$ !START_REMOTE_SERVER.COM"
$ WRITE STARTFILE "$ DEFINE/NOLOG/SYSTEM/EXEC REMOTESERVEREXE ''FOLDER'.EXE]"
$ WRITE STARTFILE "$ DEFINE/NOLOG/SYSTEM/EXEC REMOTESERVERLOG ''FOLDER'.LOG]"
$ WRITE STARTFILE "$ !"
$ WRITE STARTFILE "$ RUN/DETACHED SYS$SYSTEM:LOGINOUT.EXE -"
$ WRITE STARTFILE "        /INPUT=REMOTESERVEREXE:REMOTESERVER.COM -"
$ WRITE STARTFILE "        /OUTPUT=REMOTESERVERLOG:REMOTESERVER.LOG -"
$ WRITE STARTFILE "        /OUTPUT=REMOTESERVERLOG:REMOTESERVER.LOG -"
$ WRITE STARTFILE "        /UIC=SYSTEM -"
$ WRITE STARTFILE "        /PROCESS_NAME="RemoteServer""
$ WRITE STARTFILE "$ !"
$ WRITE STARTFILE "$ EXIT"
$ CLOSE STARTFILE
$
$ OPEN/WRITE STARTFILE REMOTESERVEREXE:REMOTESERVER.COM
$ WRITE STARTFILE "$!REMOTESERVER.COM"
$ WRITE STARTFILE "$ REMOTE_SERVER:==$REMOTESERVEREXE:REMOTESERVER.EXE"
$ WRITE STARTFILE "$ REMOTE_SERVER -P 30001"
$ WRITE STARTFILE "$ EXIT"
$ CLOSE STARTFILE
$
$ENDIF
$
$DONE:
$ EXIT

